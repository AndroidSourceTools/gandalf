buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.btkelly:gnag:2.4.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'gnag'
apply from: 'variables.gradle'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName libraryVersion
        consumerProguardFiles 'proguard-rules.pro'
    }

    lintOptions {
        abortOnError false
        warning 'InvalidPackage'
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            maxParallelForks = 2
            forkEvery = 150
            afterTest { descriptor, result ->
                println "Executing test for ${descriptor.name} with result: ${result.resultType}"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply from: 'bintray.gradle'

gnag {
    checkstyle {
        enabled true
        reporterConfig project.file('config/checkstyle.xml')
    }
    pmd {
        enabled true
        reporterConfig project.file('config/pmd.xml')
    }
    github {
        repoName 'btkelly/gandalf'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.squareup.okhttp3:okhttp:4.7.2'
    implementation 'com.google.code.gson:gson:2.8.6'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.3.3'
}